@AbapCatalog.sqlViewName: 'ZCDSSTOPO'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Pedidos Transferencias- Iten e remessas'
@Metadata.ignorePropagatedAnnotations: true
define view ZCDS_PO_TRANSFER as select from ekko
    inner join ekpo on ekpo.ebeln = ekko.ebeln
    left outer join  ekpv on ekpv.ebeln = ekko.ebeln
                         and ekpv.ebelp = ekpo.ebelp
    left outer join ZCDS_EWM_PACKSPEC on ZCDS_EWM_PACKSPEC.werks = ekko.reswk
                                     and ZCDS_EWM_PACKSPEC.matnr = ekpo.matnr
    left outer join lips on lips.vgbel = ekpo.ebeln
                        and lips.vgpos = lpad(ekpo.ebelp, 6, '0')
{
 
 ekko.bsart,
 ekko.ebeln,
 ekpo.ebelp,
 ekko.reswk,
 ekko.ekorg,
 ekko.ekgrp,
 lips.vbeln,
 lips.posnr,
 ekpo.knttp,
 ekpo.mtart,
 ekpo.werks,
 ekpo.matnr,
 ekpo.lgort,
 ekpv.route,
 ekpo.txz01,
 ekpo.menge,
 ekpo.meins,
 case
  when ekpo.meins = ZCDS_EWM_PACKSPEC.unit   then ekpo.menge
  when ekpo.meins <> ZCDS_EWM_PACKSPEC.unit  then cast( division( ekpo.menge , ZCDS_EWM_PACKSPEC.quan,3 )as bstmg)
 end as qtpkg, 
 
 case
  when ekpo.meins = ZCDS_EWM_PACKSPEC.unit   
        then  division( ekpo.menge , ZCDS_EWM_PACKSPEC.trgqty,4) 
  when ekpo.meins <> ZCDS_EWM_PACKSPEC.unit  
        then cast( Division( division( ekpo.menge , ZCDS_EWM_PACKSPEC.quan,4 ), ZCDS_EWM_PACKSPEC.trgqty,2) as abap.dec( 15, 4 ))
 end as qtpallet, 
 
 
 case
  when ekpo.meins = ZCDS_EWM_PACKSPEC.unit   
        then cast( division( ekpo.menge , ZCDS_EWM_PACKSPEC.trgqty,2) * g_weight as brgew)
  when ekpo.meins <> ZCDS_EWM_PACKSPEC.unit  
        then cast( Division(cast( division( ekpo.menge , ZCDS_EWM_PACKSPEC.quan,3 )as bstmg), ZCDS_EWM_PACKSPEC.trgqty,2) * g_weight as brgew)
 end as brgew, 

 
 case
  when ekpo.meins = ZCDS_EWM_PACKSPEC.unit   
        then cast( division( ekpo.menge , ZCDS_EWM_PACKSPEC.trgqty,2) * g_volume as volum)
  when ekpo.meins <> ZCDS_EWM_PACKSPEC.unit  
        then cast( Division(cast( division( ekpo.menge , ZCDS_EWM_PACKSPEC.quan,3 )as volum), ZCDS_EWM_PACKSPEC.trgqty,2) * g_volume as volum)
 end as volum, 
 
 
 ZCDS_EWM_PACKSPEC.lgnum,
 
 ZCDS_EWM_PACKSPEC.pntext,
 ZCDS_EWM_PACKSPEC.unit,
 ZCDS_EWM_PACKSPEC.quan,
 ZCDS_EWM_PACKSPEC.meins as umb,
 ZCDS_EWM_PACKSPEC.trgqty,
 ZCDS_EWM_PACKSPEC.minqty,
 ZCDS_EWM_PACKSPEC.rndqty,
 ZCDS_EWM_PACKSPEC.stackqtdy,
 ZCDS_EWM_PACKSPEC.quancla,
 ZCDS_EWM_PACKSPEC.hutyp,
 ZCDS_EWM_PACKSPEC.g_weight,
 ZCDS_EWM_PACKSPEC.n_weight,
 ZCDS_EWM_PACKSPEC.unit_gw,
 ZCDS_EWM_PACKSPEC.t_weight,
 ZCDS_EWM_PACKSPEC.unit_tw,
 ZCDS_EWM_PACKSPEC.g_volume,
 ZCDS_EWM_PACKSPEC.n_volume,
 ZCDS_EWM_PACKSPEC.unit_gv
 
}
where ekpo.pstyp = '7' //item de transferencia
  and ekko.bstyp = 'F' //Pedido
  and ekpo.loekz = '' //n√£o excluido
  and ekpo.elikz = '' //sem remessa final
  and lips.vbeln is null   
