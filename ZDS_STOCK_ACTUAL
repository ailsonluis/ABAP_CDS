@AbapCatalog.sqlViewName: 'ZDDL_STOCKACTUAL'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #CHECK
@OData.publish: true
@EndUserText.label: 'Saldo em estoque atual'
define view ZDS_STOCK_ACTUAL 
as select from matdoc
association[1..1] to I_MaterialDescription  as MaterialDescription on $projection.matnr = MaterialDescription.Material
association[1..1] to I_Material as _material on $projection.matnr = _material.Material
association[1..1] to ibusinesspartner as IVendor on $projection.lifnr_sid = IVendor.businesspartner
association[1..1] to ibusinesspartner as ICustumer on $projection.kunnr_sid = ICustumer.businesspartner
association[1..1] to ibatch as _IBatch on $projection.matnr = _IBatch.material 
                                      and $projection.werks = _IBatch.plant
                                      and $projection.charg_sid = _IBatch.batch

{

key matdoc.matnr,
key matdoc.werks,
MaterialDescription.MaterialDescription,
_material.MaterialBaseUnit,
_material.MaterialType,
matdoc.lgort_sid,
matdoc.charg_sid,
matdoc.sobkz,
lifnr_sid,
@Semantics.text: true
    
@EndUserText:{ label: 'Desc.Fornecedor',quickInfo: 'Desc.Fornecedor' }
cast ( IVendor.businesspartnerfullname as /b631/oivendor ) as Vendor,
kunnr_sid,
@EndUserText:{ label: 'Desc.Cliente',quickInfo: 'Desc.Cliente' }
cast( ICustumer.businesspartnerfullname as /b631/oicustomer)   as custumer,

matdoc.lbbsa_sid,
@EndUserText:{ label: 'Saldo',quickInfo: 'Saldo' }
sum(  matdoc.stock_qty ) as stock   ,
_IBatch.batchbysupplier,
_IBatch.manufacturedate,
_IBatch.shelflifeexpirationdate,
@EndUserText:{ label: 'Valiade em Dias',quickInfo: 'Validade Em Dias' }
case 
    when matdoc.charg_sid is not null then  DATS_DAYS_BETWEEN(_IBatch.manufacturedate,_IBatch.shelflifeexpirationdate )  
    else 0    
end as PrazoValidade,

@EndUserText:{ label: 'Dias p/ Vencimento',quickInfo: 'Dias p/ Vencimento' }
case
 when matdoc.charg_sid is not null then DATS_DAYS_BETWEEN(_IBatch.shelflifeexpirationdate, cast( $session.system_date as abap.dats ) )
 else 0 
end as DiasVencimento,

@EndUserText:{ label: 'Data atual',quickInfo: 'Data Atual' }
cast( $session.system_date as abap.dats ) as DataAtual



}

 group by matnr, MaterialDescription.MaterialDescription, 
          _material.MaterialBaseUnit,
            _material.MaterialType,
          werks, lgort_sid, charg_sid,sobkz,lifnr_sid,kunnr_sid, lbbsa_sid,
          IVendor.businesspartnerfullname, ICustumer.businesspartnerfullname,_IBatch.batchbysupplier,
_IBatch.manufacturedate,
_IBatch.shelflifeexpirationdate 

